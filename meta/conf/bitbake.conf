##################################################################
# Architecture-dependent build variables
##################################################################

BUILD_ARCH := "${@os.uname()[4]}"
BUILD_OS := "linux-gnu"
BUILD_VENDOR = "pc"
BUILD_SYS = "${BUILD_ARCH}-${BUILD_VENDOR}-${BUILD_OS}"

HOST_ARCH = "${BUILD_ARCH}"
HOST_OS = "${BUILD_OS}"
HOST_VENDOR ="${BUILD_VENDOR}"
HOST_SYS = "${BUILD_SYS}"

TARGET_ARCH = "aarch64"
TARGET_OS = "linux-gnu"
TARGET_VENDOR = "rpi"
TARGET_SYS = "${TARGET_ARCH}-${TARGET_VENDOR}-${TARGET_OS}"

include conf/local.conf

##################################################################
# Package default variables
##################################################################

P  = "${@bb.parse.BBHandler.vars_from_file(d.getVar('FILE', False),d)[0]}"
PN = "${@bb.parse.BBHandler.vars_from_file(d.getVar('FILE', False),d)[0]}"
PV = "${@bb.parse.BBHandler.vars_from_file(d.getVar('FILE', False),d)[1] or '1.0'}"
PR = "${@bb.parse.BBHandler.vars_from_file(d.getVar('FILE', False),d)[2] or '0'}"
PF = "${PN}-${PV}-${PR}"

SRC_URI = ""
PV_SRC = "${@d.getVar('SRCREV')[:6]}"

##################################################################
# General directories for the build system
##################################################################

FILE_DIRNAME = "${@os.path.dirname(d.getVar('FILE', False))}"

DL_DIR  ?= "${TOPDIR}/pacDownload"
PCACHE  ?= "${DL_DIR}/pkgcache"
SSTATE  ?= "${TOPDIR}/pacState"
DEPLOY  ?= "${TOPDIR}/pacDeploy"
PACBAKE ?= "${TOPDIR}/pacBake"

SSTATE  .= "/${TARGET_SYS}"
DEPLOY  .= "/${TARGET_SYS}"
PACBAKE .= "/${TARGET_SYS}"

CACHE = "${PACBAKE}/cache"
STAMP = "${PACBAKE}/stamps/${PF}"

LOCAL = "${PACBAKE}/local"
SHARE = "${PACBAKE}/share"
REPO  = "${PACBAKE}/repo"
STAGE = "${PACBAKE}/stage"

WORKDIR = "${PACBAKE}/work/${PF}"
SYSBASE = "${WORKDIR}/sysbase"
SRCBASE = "${WORKDIR}/srcbase"
SRCDIR  = "${SRCBASE}/${P}-${PV}"
SRCBASE = "${WORKDIR}/srcbase"

FILES_TARGET = "${WORKDIR}/target"
FILES_SETUP  = "${FILES_TARGET}/setup"
FILES_SHARE  = "${FILES_TARGET}/share"
FILES_DEPLOY = "${FILES_TARGET}/deploy"

W = "${WORKDIR}"
T = "${W}/temp"

##################################################################
# Build environment
##################################################################

export SDK_PREFIX  = "/sdk_${TARGET_ARCH}"
export SDK_SYSROOT = "${SDK_PREFIX}/${TARGET_SYS}/sysroot"

PATH_prepend = "${SDK_PREFIX}/bin:"
export PATH

export PKG_CONFIG_PATH = "${SDK_SYSROOT}/lib/pkgconfig:${SDK_SYSROOT}/usr/lib/pkgconfig"

CC_BUILD = "${BUILD_SYS}-gcc"
CXX_BUILD = "${BUILD_SYS}-g++"
AR_BUILD = "${BUILD_SYS}-gcc-ar"
RANLIB_BUILD = "${BUILD_SYS}-gcc-ranlib"

export CC = "${TARGET_SYS}-gcc"
export CXX = "${TARGET_SYS}-g++"
export AR = "${TARGET_SYS}-ar"
export AS = "${TARGET_SYS}-as"
export RANLIB = "${TARGET_SYS}-ranlib"

export LC_ALL = "C"

##################################################################
# Bitbake config
##################################################################

BBINCLUDELOGS = "True"
BBINCLUDELOGS_LINES = "80"

BB_LOGFMT = "log/{task}.log"
BB_VERBOSE_LOGS = "True"

BB_SIGNATURE_HANDLER = "basichash"
BB_HASHBASE_WHITELIST += "DL_DIR FILE_DIRNAME FILESPATH FILE HOME PATH PCACHE PWD SHELL SSTATE TERM TMUX USER"

BB_NUMBER_THREADS ?= "${@bb.utils.cpu_count()}"
