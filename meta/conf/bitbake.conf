##################################################################
# Architecture-dependent build variables
##################################################################

BUILD_ARCH := "${@os.uname()[4]}"
BUILD_OS := "linux-gnu"
BUILD_VENDOR = "pc"
BUILD_SYS = "${BUILD_ARCH}-${BUILD_VENDOR}-${BUILD_OS}"

HOST_ARCH = "${BUILD_ARCH}"
HOST_OS = "${BUILD_OS}"
HOST_VENDOR ="${BUILD_VENDOR}"
HOST_SYS = "${BUILD_SYS}"

TARGET_ARCH = "aarch64"
TARGET_OS = "linux-gnu"
TARGET_VENDOR = "rpi"
TARGET_SYS = "${TARGET_ARCH}-${TARGET_VENDOR}-${TARGET_OS}"

include conf/local.conf

##################################################################
# Package default variables
##################################################################

P  = "${@bb.parse.BBHandler.vars_from_file(d.getVar('FILE', False),d)[0]}"
PN = "${@bb.parse.BBHandler.vars_from_file(d.getVar('FILE', False),d)[0]}"
PV = "${@bb.parse.BBHandler.vars_from_file(d.getVar('FILE', False),d)[1] or '1.0'}"
PR = "${@bb.parse.BBHandler.vars_from_file(d.getVar('FILE', False),d)[2] or '0'}"
PF = "${PN}-${PV}-${PR}"

SRC_URI = ""
PV_SRC = "${@d.getVar('SRCREV')[:6]}"

##################################################################
# General directories for the build system
##################################################################

FILE_DIRNAME = "${@os.path.dirname(d.getVar('FILE', False))}"

DL_DIR ?= "${TOPDIR}/bbDownload"
BB_TOP ?= "${TOPDIR}"
BB_DIR ?= "${BB_TOP}/bbWork/${TARGET_ARCH}"
SSTATE ?= "${TOPDIR}/bbState/${TARGET_ARCH}"
DEPLOY ?= "${TOPDIR}/deploy/${TARGET_ARCH}"

CACHE   = "${BB_DIR}/cache"
STAMP   = "${BB_DIR}/stamps/${PF}"
COMMON  = "${BB_DIR}/common"
STAGE   = "${BB_DIR}/stage"
REPO    = "${BB_DIR}/repo"
SHARED  = "${BB_DIR}/shared"

WORKDIR = "${BB_DIR}/work/${PF}"
SYSBASE = "${WORKDIR}/sysbase"
SRCBASE = "${WORKDIR}/srcbase"
SRCDIR  = "${SRCBASE}/${P}-${PV}"
SRCBASE = "${WORKDIR}/srcbase"

FILES_TARGET  = "${WORKDIR}/target"
FILES_INSTALL = "${FILES_TARGET}/install"
FILES_SHARED  = "${FILES_TARGET}/shared"
FILES_DEPLOY  = "${FILES_TARGET}/deploy"

W = "${WORKDIR}"
T = "${W}/temp"

##################################################################
# Build environment
##################################################################

export LC_ALL = "C"

CC_BUILD = "${BUILD_SYS}-gcc"
CXX_BUILD = "${BUILD_SYS}-g++"
AR_BUILD = "${BUILD_SYS}-gcc-ar"
RANLIB_BUILD = "${BUILD_SYS}-gcc-ranlib"

export XPATH = "/xtools-${TARGET_ARCH}"
export XROOT = "${XPATH}/${TARGET_SYS}/sysroot"

PATH_prepend = "${XPATH}/bin:"
export PATH

export CC = "${TARGET_SYS}-gcc"
export CXX = "${TARGET_SYS}-g++"
export AR = "${TARGET_SYS}-ar"
export AS = "${TARGET_SYS}-as"
export RANLIB = "${TARGET_SYS}-ranlib"

##################################################################
# Bitbake config
##################################################################

BB_LOGFMT = "log/{task}.log"
BB_VERBOSE_LOGS = "True"

BB_SIGNATURE_HANDLER = "basichash"
BB_HASHBASE_WHITELIST += "DL_DIR FILE_DIRNAME FILESPATH FILE HOME PATH PWD SHELL SSTATE TERM TMUX USER"

BB_NUMBER_THREADS ?= "${@bb.utils.cpu_count()}"
